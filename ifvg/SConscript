#!/usr/bin/python
# -*- coding: utf-8 -*-
# ==========================================================================
# IFVG - Ionflux' Volumetric Graphics Library
# Copyright © 2014 Jörn P. Meier
# mail@ionflux.org
# --------------------------------------------------------------------------
# SConscript                  SCons build script
# ==========================================================================
# 
# This file is part of IFVG - Ionflux' Volumetric Graphics Library.
# 
# IFVG - Ionflux' Volumetric Graphics Library is free software; you can 
# redistribute it and/or modify it under the terms of the GNU General 
# Public  License as published by the Free Software Foundation; either 
# version 2 of the License, or (at your option) any later version.
# 
# IFVG - Ionflux' Volumetric Graphics Library is distributed in the hope 
# that it will be useful, but WITHOUT ANY WARRANTY; without even the 
# implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# See the GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with IFVG - Ionflux' Volumetric Graphics Library; if not, write to 
# the Free Software Foundation, Inc., 59 Temple Place, Suite 330, Boston, 
# MA 02111-1307 USA
#
# ==========================================================================
import os, subprocess, shutil, shlex, string, copy, re, sys

# Create a default local configuration file if it does not exist.
if (not os.path.isfile('site_scons/local_config.py')):
    shutil.copy('site_scons/local_config.py.default', 
        'site_scons/local_config.py')

import local_config as lc
from sources import *
from builders import buildSource, buildClassInterface, buildInterface
from builders import buildClassXMLFactory

Help("""
Type 'scons' to build the default targets.
Type 'scons install' to install to default location.
Type 'scons <target>' to build a specific target.

You can add local configuration to site_scons/local_config.py.

Targets:
  (default)                    Everything.
  all                          Everything
  libs                         Shared libraries.
  programs                     Programs.
  tests                        Test programs.
  sources                      Source files (requires ifclassgen).
  interfaces                   SWIG interface declarations (requires iftpl).
  xmlio                        XML I/O functions.
  pymodule                     Python module.
  install                      Install headers, libraries and programs.

Options:
  prefix=<target directory>    Installation prefix
                               (default: /usr/local)
  debug=<yes|no>               Whether to use debug compiler flags 
                               (default: yes)
""")

env0 = os.environ
env0['PATH'] += (':' + lc.PATH)

env = Environment(ENV = env0, 
    CPPPATH = ['include'] + lc.CPPPATH, 
    LIBPATH = ['lib'] + lc.LIBPATH)
env.MergeFlags(['-Wall', '-Wno-deprecated', '-O2'])

if (hasattr(lc, 'debug') and lc.debug):
    debugDefault = 'yes'
else:
    debugDefault = 'no'
if (ARGUMENTS.get('debug', debugDefault) == 'yes'):
    env.Append(CCFLAGS = '-g')

ifvgEnv = env.Clone()

if (hasattr(lc, 'prefix')):
    defaultPrefix = lc.prefix
else:
    defaultPrefix = '/usr/local'
prefix = ARGUMENTS.get('prefix', defaultPrefix)

ifvgEnv.VariantDir('build', 'src')
ifvgEnv.VariantDir('build/test', 'test')
ifvgEnv.VariantDir('build/swig/wrapper', 'swig/wrapper')

def checkProgram(ctx, cmd):
    """Check for installed program."""
    ctx.Message('Checking for ' + cmd + '... ')
    try:
        subprocess.call(cmd, stdout=subprocess.PIPE, 
            stderr=subprocess.STDOUT)
        result = 1
    except Exception:
        result = 0
    ctx.Result(result)
    return result

def checkPkgConfig(ctx, pkg):
    """Check for package using pkg-config."""
    ctx.Message('Checking for ' + pkg + '... ')
    cmd = "pkg-config --cflags " + pkg
    try:
        p0 = subprocess.Popen(shlex.split(cmd), stdout=subprocess.PIPE, 
            stderr=subprocess.STDOUT)
        d = p0.stdout.read()
        if (d.find('was not found') != -1):
            result = 0
        else:
            ctx.sconf.env.MergeFlags('!' + cmd)
            result = 1
    except Exception:
        result = 0
    ctx.Result(result)
    return result

config = ifvgEnv.Configure(custom_tests = { 
    "CheckProgram" : checkProgram, 
    "CheckPkgConfig" : checkPkgConfig })

missingDeps = []
if (not config.CheckPkgConfig("sigc++-2.0")):
    missingDeps += ['sigc++']
else:
    ifvgEnv.MergeFlags(['!pkg-config --cflags --libs sigc++-2.0'])
if (not config.CheckLibWithHeader("ifobject", "ifobject/IFObject.hpp", 
    'C++', autoadd = 0)):
    missingDeps += ['ifobject']
if (not config.CheckLibWithHeader("ifmapping", "ifmapping/ifmapping.hpp", 
    'C++', autoadd = 0)):
    missingDeps += ['ifmapping']
if (not config.CheckPkgConfig("gdk-pixbuf-2.0")):
    missingDeps += ['gdk-pixbuf-2.0']
if (not config.CheckPkgConfig("cairo")):
    missingDeps += ['cairo']
if (not config.CheckPkgConfig("librsvg-2.0")):
    missingDeps += ['rsvg']
if (not config.CheckLibWithHeader("altjira", "altjira/altjira.hpp", 
    'C++', autoadd = 0)):
    missingDeps += ['altjira']
if (not config.CheckLibWithHeader("geoutils", "geoutils/geoutils.hpp", 
    'C++', autoadd = 0)):
    missingDeps += ['geoutils']
if (not config.CheckLibWithHeader("altjirageoutils", 
    "geoutils/altjirageoutils.hpp", 'C++', autoadd = 0)):
    missingDeps += ['altjirageoutils']
if (not config.CheckLibWithHeader("geoutilsgl", 
    "geoutils/geoutilsgl.hpp", 'C++', autoadd = 0)):
    missingDeps += ['altjirageoutils']
if (not config.CheckLibWithHeader("fbxsdk", 
    "fbxsdk.h", 'C++', autoadd = 0)):
    missingDeps += ['fbxsdk']

if (len(missingDeps) > 0):
    print "***********************************************************"
    print "IFVG cannot be built because the following dependencies"
    print "were not found:"
    print " "
    for it in missingDeps:
        print "  " + it
    print " "
    print "***********************************************************"
    Exit(1)

buildPyModule = True
missingDeps = []
if (not config.CheckLibWithHeader("python" + lc.pythonVersion, 
    "Python.h", 'C', autoadd = 0)):
    print "***************************************************"
    print "Python not found - Python modules will NOT be built!"
    print "***************************************************"
    buildPyModule = False

missingDeps = []
buildViewerLib = True
if (not config.CheckLibWithHeader("glfw", "GLFW/glfw3.h", 'C', 
    autoadd = 0)):
    missingDeps += ["GLFW"]
if (not config.CheckLibWithHeader("GL", "GL/gl.h", 'C', autoadd = 0)):
    missingDeps += ["OpenGL"]
if (not config.CheckLibWithHeader("glut", "GL/glut.h", 'C', autoadd = 0)):
    missingDeps += ["GLUT"]
if (not config.CheckLibWithHeader("glad", "glad/glad.h", 'C', autoadd = 0)):
    missingDeps += ["GLUT"]

if (len(missingDeps) > 0):
    print "********************************************************"
    print "IFVG/Viewer  will NOT be built because the following "
    print "dependencies were not found:"
    print " "
    for it in missingDeps:
        print "  " + it
    print " "
    print "********************************************************"
    buildViewerLib = False

haveIFClassgen = True
if (not config.CheckProgram("ifclassgen0")):
    haveIFClassgen = False
haveIFTpl = True
if (not config.CheckProgram("iftpl0")):
    haveIFTpl = False
haveSWIG = True
if (not config.CheckProgram("swig")):
    print "*************************************************"
    print "SWIG not found - Python module will NOT be built!"
    print "*************************************************"
    haveSWIG = False
    buildPyModule = False

ifvgEnv = config.Finish()

sourceBuilder = Builder(action = buildSource)
classInterfaceBuilder = Builder(action = buildClassInterface)
interfaceBuilder = Builder(action = buildInterface)
swigIncludes = ''
for it in ifvgEnv['CPPPATH']:
    swigIncludes += ('-I' + it + ' ')
swigPythonBuilder = Builder(action = "swig -c++ -python "
    + swigIncludes + "-outdir swig/python -o $TARGET $SOURCE")
classXMLFactoryBuilder = Builder(action = buildClassXMLFactory)

ifvgEnv.Append(BUILDERS = { 'Source' : sourceBuilder, 
    'ClassInterface' : classInterfaceBuilder, 
    'Interface' : interfaceBuilder, 
    'SWIGPython' : swigPythonBuilder, 
    'ClassXMLFactory' : classXMLFactoryBuilder
})

ifvgEnv.MergeFlags([
    '!pkg-config --cflags --libs gdk-pixbuf-2.0', 
    '!pkg-config --cflags --libs cairo', 
    '!pkg-config --cflags --libs librsvg-2.0'])

ifvgViewerEnv = ifvgEnv.Clone()
if (buildViewerLib):
    ifvgViewerEnv.Append(LIBS = ['glfw', 'GL', 'glut', 'glad'])

allClasses = (ifvgLibClasses + ifvgViewerLibClasses)
allTargets = []

# Source files.
if (haveIFClassgen):
    sources = []
    for it in allClasses:
        sources += [ 
            ifvgEnv.Source(target = [
                    'include/ifvg/' + it + '.hpp', 
                    'src/' + it + '.cpp'], 
                source = ['conf/class/' + it + '.conf'])
        ]
    allTargets += sources
else:
    print "**********************************************************"
    print "ifclassgen not found - source files will NOT be generated!"
    print "**********************************************************"

# XML object factory configuration files.
if (haveIFTpl):
    classXMLFactory = []
    # ifvg library
    for it in ifvgLibXMLClasses:
        t0 = 'conf/class/xmlio/' + it + 'XMLFactory.conf'
        classXMLFactory += [
            ifvgEnv.ClassXMLFactory(target = t0, 
                source = ['conf/class/' + it + '.conf']), 
        ]
    ## IFVG/Viewer library
    # Nothing here.
    allTargets += classXMLFactory
else:
    print "***********************************************************"
    print "iftpl not found -  XML factory configuration files will NOT "
    print "be generated!"
    print "***********************************************************"

# XML object factory source files.
xmlFactoryHeaders = []
if (haveIFClassgen):
    xmlFactorySources = []
    for it in ifvgLibXMLClasses:
        t0 = 'include/ifvg/xmlio/' + it + 'XMLFactory.hpp'
        t1 = 'src/xmlio/' + it + 'XMLFactory.cpp'
        xmlFactorySources += [ 
            ifvgEnv.Source(target = [ t0, t1 ], 
                source = ['conf/class/xmlio/' + it + 'XMLFactory.conf'])
        ]
        xmlFactoryHeaders += [t0]
        ifvgLibSources += [ 'build/xmlio/' + it + 'XMLFactory.cpp' ]
    allTargets += xmlFactorySources
else:
    print "**********************************************************"
    print "ifclassgen not found - XML factory source files will NOT "
    print "be generated!"
    print "**********************************************************"

# Interface declarations.
if (haveIFTpl):
    interfaces = []
    classInterfaces = []
    # ifvg library
    interfaceTargets = []
    for it in ifvgLibClasses:
        t0 = 'swig/classes/' + it + '.i'
        interfaceTargets += [t0]
        classInterfaces += [ 
            ifvgEnv.ClassInterface(target = [ t0 ], 
                source = ['conf/class/' + it + '.conf'])
        ]
    # ifvg library: XML object factories
    for it in ifvgLibXMLClasses:
        t0 = 'swig/classes/xmlio/' + it + 'XMLFactory.i'
        interfaceTargets += [t0]
        classInterfaces += [ 
            ifvgEnv.ClassInterface(target = [ t0 ], 
                source = ['conf/class/xmlio/' + it + 'XMLFactory.conf'])
        ]
    interfaces += [ ifvgEnv.Interface(target = lc.swigInterfaceFile[0], 
        source = interfaceTargets + [lc.swigInterfaceTemplateFile[0]]) ]
    # IFVG/Viewer library: classes
    interfaceTargets = []
    for it in ifvgViewerLibClasses:
        t0 = 'swig/classes/' + it + '.i'
        interfaceTargets += [t0]
        classInterfaces += [ 
            ifvgViewerEnv.ClassInterface(target = [ t0 ], 
                source = ['conf/class/' + it + '.conf'])
        ]
    interfaces += [ ifvgViewerEnv.Interface(target = lc.swigInterfaceFile[1], 
        source = interfaceTargets + [lc.swigInterfaceTemplateFile[1]]) ]
    allTargets += (classInterfaces + interfaces)
else:
    print "****************************************************************"
    print "iftpl not found -  Interface declarations will NOT be generated!"
    print "****************************************************************"

# ifvg shared library
ifvgLibEnv = ifvgEnv.Clone()
ifvgLibEnv.Append(LIBS = ['ifobject', 'tinyxml', 'ifmapping', 'altjira', 
    'geoutils', 'altjirageoutils', 'fbxsdk'])
ifvgLib = ifvgLibEnv.SharedLibrary(target = "lib/ifvg", 
    source = ifvgLibSources)

# IFVG/Viewer shared library
ifvgViewerLibEnv = ifvgViewerEnv.Clone()
ifvgViewerLibEnv.Append(LIBS = ['ifobject', 'tinyxml', 'ifmapping', 
    'altjira', 'altjiradraw', 'geoutils', 'geoutilsgl', 'ifvg'])
ifvgViewerLib = ifvgViewerLibEnv.SharedLibrary(
    target = "lib/ifvgviewer", 
    source = ifvgViewerLibSources)

targetLibs = [ifvgLib]
if (buildViewerLib):
    targetLibs += [ifvgViewerLib]

# programs.
targetBins = []

# tests
testEnv = ifvgEnv.Clone()
testEnv.Append(LIBS = ['ifobject', 'tinyxml', 'ifmapping', 
    'altjira', 'altjiradraw', 'geoutils', 'geoutilsgl', 'ifvg'])

testSerialization01Bin = testEnv.Program('test/bin/test_serialization_01', 
	['build/test/test_serialization_01.cpp'])

targetTests = [testSerialization01Bin]

# ifvg Python module.
ifvgWrapper = ifvgEnv.SWIGPython(target = [ 
        'swig/wrapper/ifvg_wrap.cpp', 'swig/python/CIFVG.py'], 
    source = lc.swigInterfaceFile[0])

ifvgPyModuleEnv = ifvgEnv.Clone()
ifvgPyModuleEnv.Append(LIBS = ['ifvg', 'python' + lc.pythonVersion])
ifvgPyModuleEnv.Append(LIBS = ['ifobject', 'ifmapping', 'altjira', 
    'geoutils', 'geoutilsfbx', 'altjirageoutils'])

ifvgPyModule = ifvgPyModuleEnv.SharedLibrary(
    target = 'swig/python/_CIFVG', 
    source = 'build/swig/wrapper/ifvg_wrap.cpp', 
    SHLIBPREFIX = '', SHLIBSUFFIX = '.so')

# IFVG/Viewer Python module.
ifvgViewerLibWrapper = ifvgViewerEnv.SWIGPython(target = [ 
        'swig/wrapper/ifvgviewer_wrap.cpp', 
        'swig/python/CIFVGViewer.py'], 
    source = lc.swigInterfaceFile[1])

ifvgViewerLibPyModuleEnv = ifvgViewerEnv.Clone()
ifvgViewerLibPyModuleEnv.Append(LIBS = ['ifobject', 'ifmapping', 
    'geoutils', 'geoutilsfbx', 'ifvg', 'ifvgviewer', 
    'python' + lc.pythonVersion])

ifvgViewerLibPyModule = ifvgViewerLibPyModuleEnv.SharedLibrary(
    target = 'swig/python/_CIFVGViewer', 
    source = 'build/swig/wrapper/ifvgviewer_wrap.cpp', 
    SHLIBPREFIX = '', SHLIBSUFFIX = '.so')

pyWrappers = [ifvgWrapper]
pyModules = [ifvgPyModule]
if (buildViewerLib):
    pyWrappers += [ifvgViewerLibWrapper]
    pyModules += [ifvgViewerLibPyModule]

allHeaders = ifvgLibHeaders + lc.swigInterfaceFile
if (buildViewerLib):
    allHeaders += [ifvgViewerLibHeaders]

installBinTargets = [targetBins]

installHeaders = ifvgEnv.Install(prefix + '/include/' + lc.baseName, 
    allHeaders)
installXMLFactoryHeaders = ifvgEnv.Install(
    prefix + '/include/xmlio/' + lc.baseName, 
    xmlFactoryHeaders)
installLibs = ifvgEnv.Install(prefix + '/lib', targetLibs)
installBins = ifvgEnv.Install(prefix + '/bin', installBinTargets)
if (buildPyModule):
    pyModuleTargets = ['swig/python/CIFVG.py', ifvgPyModule]
    if (buildViewerLib):
        pyModuleTargets += ['swig/python/CIFVGViewer.py', 
            ifvgViewerLibPyModule]
    installPyModules = ifvgEnv.Install(
        prefix + lc.pythonSitePackagesPath, pyModuleTargets)

allTargets += (targetLibs + targetBins + targetTests)
if (buildPyModule):
    allTargets += (pyWrappers + pyModules)

installTargets = [installHeaders, installXMLFactoryHeaders, 
    installLibs, installBins]
if (buildPyModule):
    installTargets += [installPyModules]

aliasLibs = Alias('libs', targetLibs)
aliasBins = Alias('programs', targetBins)
aliasTests = Alias('tests', targetTests)
aliasAll = Alias('all', allTargets)
aliasInstall = Alias('install', installTargets)
if (haveIFClassgen):
    aliasSources = Alias('sources', sources)
if (haveIFTpl):
    aliasXMLIO = Alias('xmlio', classXMLFactory + xmlFactorySources)
    aliasInterfaces = Alias('interfaces', classInterfaces + interfaces)
if (buildPyModule):
    aliasPyModule = Alias('pymodule', pyModules)

Default('all')
